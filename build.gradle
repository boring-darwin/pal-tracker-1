import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    ext {
        springBootVersion = "2.0.6.RELEASE"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}


plugins {
    id "java"
    id "org.flywaydb.flyway" version "5.0.5"
}

apply plugin: 'org.springframework.boot'

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
    testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.7")
    testCompile("org.mockito:mockito-core:2.23.4")
    compile("org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion")
    compile("mysql:mysql-connector-java:6.0.6")
}

def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"

bootRun.environment([
        "welcome.message": "hello",
        "SPRING_DATASOURCE_URL": developmentDbUrl,
])

def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
test.environment([
        "welcome.message": "Hello from test",
        "SPRING_DATASOURCE_URL": testDbUrl,
])

flyway {
    url = developmentDbUrl
    user = "tracker"
    password = ""
    locations = ["filesystem:databases/tracker/migrations"]
}

task testMigrate(type: FlywayMigrateTask) {
    url = testDbUrl
}